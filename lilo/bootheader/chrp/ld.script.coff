/* $Id$ */
OUTPUT_ARCH(powerpc)
SECTIONS
{
	/*
	 * OldWorld PowerMacs need alignment in COFF image
	 * they also like only .text, .data and .bss sections
	 */
	.text	:
	{
		*(.text)
		*(.fixup)
		__relocate_start = .;
		*(.relocate_code)
		__relocate_end = .;
	}
	_etext = .;
	. = ALIGN(4096);
	.data	:
	{
		*(.data*)
		*(.sdata*)
		*(.rodata*)
		__got2_start = .;
		*(.got2)
		__got2_end = .;

		_uts_string_start = .;
		*(.uts_string)
		_uts_string_end = .;

		_vmlinuz_start = .;
		/* if this sections gets renamed, update also the mkzimage scripts */
		*(.kernel:vmlinux.strip)
		_vmlinuz_end = .;

		_initrd_start = .;
		/* if this sections gets renamed, update also the mkzimage scripts */
		*(.kernel:initrd)
		_initrd_end = .;
	}
	_edata = .;
	. = ALIGN(4096);
	__bss_start = .;
	.bss	:
	{
		*(.bss)
		*(.sbss)
	}
	_end = .;
}
