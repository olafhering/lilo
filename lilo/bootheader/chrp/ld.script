/* $Id$ */
OUTPUT_ARCH(powerpc)
SECTIONS
{
	.text	:
	{
		*(.text)
		*(.fixup)
		__relocate_start = .;
		*(.relocate_code)
		__relocate_end = .;
	}
	_etext = .;
	. = ALIGN(4096);
	.data	:
	{
		*(.data*)
		*(.sdata*)
		*(.rodata*)
		__got2_start = .;
		*(.got2)
		__got2_end = .;

		_uts_string_start = .;
		*(.uts_string)
		_uts_string_end = .;

	}
	.kernel:vmlinux.strip	:
	{
		_vmlinuz_start = .;
		*(.kernel:vmlinux.strip)
		_vmlinuz_end = .;
	}
	.kernel:initrd	:
	{
		_initrd_start = .;
		*(.kernel:initrd)
		_initrd_end = .;
	}
	_edata = .;
	. = ALIGN(4096);
	__bss_start = .;
	.bss	:
	{
		*(.bss)
		*(.sbss)
	}
	_end = .;
}
